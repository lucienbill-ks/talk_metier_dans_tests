params scriptDirPath:empty

// Ce script importe tout les "steps"
// Les sous-dossiers sont gérés

if scriptDirPath is empty
	set errmsg to <<Please specify a directory that contains functions to import
Example:
	the resolvedfilePath of ( ResourcePath() & "/../Scripts/steps")
	  (note : this will import all functions defined in the specified directory, and its subdirectories)
>>

	Throw "importAllFunctions: missing mandatory parameter", errmsg
end if

set listOfStepFiles to files of scriptDirPath
set listOfSubfolders to the folders of scriptDirPath

Repeat with each item myFile of listOfStepFiles
	importFunctionsOfFile scriptDirPath & "/" & myFile
End repeat

Repeat with each item mySubfolder of listOfSubfolders
	run importAllFunctions scriptDirPath & "/" & mySubfolder
End repeat



to importFunctionsOfFile myFile
	Try
		Start using myFile
	Catch theException
		
		throw "Impossible d'importer les fonctions", !"Le fichier '[[myFile]]' n'est pas reconnu ou n'existe pas"
	End try
end importFunctionsOfFile