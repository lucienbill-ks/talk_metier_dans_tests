// These are some custom functions to exten SenseTalk's native capabilitie

// =======================
// CUSTOM FUNCTIONS
// =======================

to cliquer text_or_img_or_options:empty, moreOptions:{}
	set options to "_lib/eggplant/_lib"._parse_parameters_for_image_or_txt_search(text_or_img_or_options, moreOptions)
	click options
end cliquer


to cliquerDroit with text_or_img_or_options:empty, moreOptions:{}
	set options to "_lib/eggplant/_lib"._parse_parameters_for_image_or_txt_search(text_or_img_or_options, moreOptions)
	RightClick options
end cliquerDroit

to attendre with text_or_img_or_options, moreOptions:{}
	set params to "_lib/eggplant/_lib"._parse_parameters_for_image_or_txt_search_waitfor(text_or_img_or_options, moreOptions)
	waitfor the first item of params, the second item of params
	// log "waitfor" &&  the first item of params &"," && the second item of params // DEBUG
end attendre

to logFct 
	// Goal: to log the entering / exiting of functions
	// At the beginging of the function (such as: to myfunction param_1, param_2), call it like this:
	// logFct param_1, param_2
	//
	// At the end of the function, just call it again
	// logFct
	//
	// caveat: if you use try/catch when calling a function that uses logFct, do this:
	//
	// Try
	// 	myfunction "this will fail"
	// Catch theException
	// 	delete the last item of global fct_stack
	// 	delete the last item of global param_stack
	// 	
	// 	// Then do your things
	// End try
	
	
	global fct_stack, param_stack
	if fct_stack is empty then 
		set fct_stack to []
		set param_stack to []
	end if
	
	// build the string to display
	set current_function to the Handler of the second-to-last item of the callStack 
	
	// Are we entering or leaving the function?
	set is_entering_function to (the last item of fct_stack is not current_function)
	
	if is_entering_function then
		set param_str to the params
		delete param(0) from param_str
		
		// PREFIX
		set identPrefix to ""
		set nb_iterations to the number of items of fct_stack
		Repeat nb_iterations times
			set identPrefix to identPrefix && "|"
		End repeat
		if nb_iterations is greater than -1 then set identPrefix to identPrefix && "|⎻"
		set prefix to identPrefix // && "Func START: "
		// --------------
		
		if param_str is not << "">> then
			log prefix && current_function && param_str
		else
			log prefix && current_function
		end if
		insert current_function into fct_stack
		insert param_str into param_stack
	else // we are leaving the function
		set param_str to the last item of param_stack
		
		// PREFIX
		set identPrefix to ""
		set nb_iterations to the number of items of fct_stack
		Repeat nb_iterations -1 times
			set identPrefix to identPrefix && "|"
		End repeat
		if nb_iterations is greater than 0 then set identPrefix to identPrefix && "|_"
		set prefix to identPrefix // && "Func END  : "
		// --------------
		
		logSuccess prefix && current_function & " (OK)"
		
		delete the last item of fct_stack
		delete the last item of param_stack
	end if
end logFct


to get_rectangle_between_2_elements topLeft_element:empty, bottomRightElement:empty
	// This function draws a rectangle between top left of first element and bottom right of second element
	// Each element can be a text or an image, but no fancy OCR options.
	// If you need something fancier, use something else
	
	do_waitfor topLeft_element
	set TL to FoundImageInfo().imageRectangle.topLeft
	do_waitfor bottomRightElement
	set BR to FoundImageInfo().imageRectangle.bottomRight
	
	return [TL, BR]
end get_rectangle_between_2_elements

// ==============================
// TWEAKING STANDARD SENSETALK FUNCTIONS
// ==============================


to click myparameters
	Try
		put "click" && myparameters
		send click myparameters to SenseTalk
		// wait 0.5
	Catch theException
		throw theException // Goal: make use of the extra stack info of the modified "Throw" function
	End try
end click

to RightClick myparameters
	Try
		put "RightClick" && myparameters
		send RightClick myparameters to SenseTalk
		// wait 0.5
	Catch theException
		throw theException // Goal: make use of the extra stack info of the modified "Throw" function
	End try
end RightClick

to waitfor timeout, arguments
	Try
		put "waitfor" && timeout & "," && arguments
		send waitfor timeout, arguments to SenseTalk
	Catch theException
		throw theException // Goal: make use of the extra stack info of the modified "Throw" function
	End try
end waitfor

to ImageRectangle myparam
	// rewriting this function so that it has a default timeout of 30 seconds
	set timeout to 30
	
	// myparam is either a property list, or the path of an image
	if myparam is a property list then
		add properties {waitfor:timeout} to myparam
	else
		set myparam to {imageName:myParam, waitfor:timeout}
	end if
	
	Try
		if not ImageFound(myParam) then
			Throw "Image not found", myParam
		end if
		return FoundImageInfo().imageRectangle
	Catch theException
		throw theException
	End try
	
end ImageRectangle

(*
to Throw with theException, thereason
	// This is a rewrite of the standard "Throw" function,
	// but it writes addisional callstack information in the error message
	
	set stk to the callStack
	delete the last item of stk
	set my_stack to "Call stack:" & newline
	
	set indent_spacer to ""
	Repeat with each item stk_element of stk
		if the repeatIndex is 1 then
			set my_stack to my_stack & stk_element.Handler & ".script"
		else
			set scriptName to stk_element.ScriptObjectID
			delete "<SOURCE>" from scriptName
			delete "<SELECTION>" from scriptName
			set my_stack to my_stack & newline & indent_spacer & scriptName & " > " & stk_element.Handler
		end if
		
		set my_stack to my_stack && "(script line " & stk_element.ScriptLine &" ; function line " && stk_element.line & ")"
		
		set indent_spacer to indent_spacer & "   "
	End repeat
	
	set thereason to thereason & newline & newline & my_stack
	
	send throw theException, thereason to SenseTalk
end Throw
*)

to ScrollWheelUp scrollAmount
	send ScrollWheelUp scrollAmount to SenseTalk
	wait 0.5 // short "hard wait" to give time to the UI to stop moving
end ScrollWheelUp

to ScrollWheelDown scrollAmount
	send ScrollWheelDown scrollAmount to SenseTalk
	wait 0.5 // short "hard wait" to give time to the UI to stop moving
end ScrollWheelDown