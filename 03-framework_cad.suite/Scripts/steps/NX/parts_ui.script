to createNewSketch
	log "create a new Sketch..."
	selectActionFromTopRibbon "Sketch"
	wait 1
	WaitFor 30, text:"Create Sketch"
	set TL to FoundImageLocation() + [0, 150]
	set BR to TL + [300, 300]
	Click text:"OK", waitFor:5, SearchRectangle:[TL,BR]
	logSuccess "create a new Sketch: OK"
end createNewSketch

to drawARectangle
	log "draw a rectangle..."
	selectActionFromTopRibbon "Rectangle"
	wait 0.5
	Click {Image:"CAD/sketch_center", WaitFor:4}
	wait 0.5
	click FoundImageLocation() + [400,-200]
	wait 0.5
	selectActionFromTopRibbon "Finish"
	logSuccess "draw a rectangle: OK"
end drawARectangle

to extrudeRectangle
	log "extrude it 42 mm..."
	selectActionFromTopRibbon "Extrude"
	wait 1
	// Between "Limits" and "Boolean", find the End Distance and set it to 42 mm
	WaitFor 4, text:"Limits", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	set TL to FoundImageInfo().imageRectangle.bottomLeft + [-50, 0]
	WaitFor 4, text:"Boolean", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	set BR to FoundImageInfo().imageRectangle.topRight + [50, 0]

	set allInstancesOfDistance to EveryImageRectangle(text:"Distance", SearchRectangle:[TL,BR])
	set repere to (the last item of allInstancesOfDistance).centerRight + [150, 0]
	doubleClick repere
	typeText "42 "
	WaitFor 4, text:"42", SearchRectangle:[repere + [-100,-25], repere + [50, 25]]
	Click text:"OK", SearchRectangle: "_lib\NX\utils".nxZones("mainZone"), waitfor:2
	LogSuccess "extrude it 42 mm: OK"
end extrudeRectangle

to measureExtrusion
	Log "measure the extrusion..."
	orientView "Isometric", true
	wait 1
	WaitFor 8.0, "CAD/myFirstPart"
	Click text:"Analysis", waitFor:0, SearchRectangle: "_lib\NX\utils".nxZones("topMenu")
	selectActionFromTopRibbon "Measure"
	wait 1
	Click (Image:"CAD/myFirstPart_segmentToMeasure", WaitFor:0)
	
	// Find the measure
	WaitFor 4, text:"42.0000", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	set y_pos_of_lenght_lim_min to FoundImageLocation().y - 10
	set y_pos_of_lenght_lim_max to FoundImageLocation().y + 10
	set ir to FoundImageInfo().imageRectangle
	set TL to ir.topLeft + [-200, -25]
	set BR to ir.bottomLeft + [ 0, 25]

	// Verify that the value found correspond to the Length
	WaitFor 0, text:"Length", searchRectangle:[TL, BR]
	if FoundImageLocation().y is between y_pos_of_lenght_lim_min and y_pos_of_lenght_lim_max then
		logSuccess "measurement is length"
	else
		logError "measure the extrusion: FAIL, please check the screenshot"
	end if
	
	Click text:"Cancel", SearchRectangle: "_lib\NX\utils".nxZones("mainZone"), waitfor:0
	
	logSuccess "measure the extrusion: OK"
end measureExtrusion

to cutCircleIntoThePiece

	log "### Add a hole, that has a specific diameter... ###"
	// TODO : add a hole measure it, modify diameter, measure again
	log "Start a new Sketch on the top plane of the part..."
	waitfor 4, imageName:"CAD/myFirstPart_segmentToMeasure"
	set repere to FoundImageLocation() + [+0, -150]

	Click text:"Home", waitFor:0, SearchRectangle: "_lib\NX\utils".nxZones("topMenu")
	selectActionFromTopRibbon "Sketch"
	click repere
	// Check that the top plane of the extrusion has been selected
	WaitFor 8.0, "CAD/myFirstPart_skecth_topPlaneSelected"
	Click text:"OK", SearchRectangle: "_lib\NX\utils".nxZones("mainZone"), waitfor:2
	LogSuccess "Start a new Sketch on the top plane of the part: OK"
	
	log "Drawing the circle..."
	wait 2 // Animation
	selectActionFromTopRibbon "Circle"
	Click (Image:"CAD/sketch_center_topPlaneOfFirstPart", WaitFor:10)
	set repere to FoundImageLocation() + [200, -200]
	Click repere
	LogSuccess "Drawing the circle: OK"
	
	log "Adding a specific diameter..."
	typeText escapeKey
	Click repere
	Click (Image:"CAD/phi_diameter", WaitFor:4)
	wait 1
	typeText "55", returnKey
	WaitFor 5, text:"Scale Sketch On First Dimension", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	typeText returnKey
	selectActionFromTopRibbon "Finish"
	wait 2 // Animation
	WaitFor 8.0, "CAD/myFirstPart_withCircleOnTop"
	
	
	LogSuccess "Adding a specific diameter: OK"
	
	log "Subtracting matter to the part..." // It's actually an extrusion
	selectActionFromTopRibbon "Extrude"
	WaitFor 4, text:"Limits", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	set TL to FoundImageInfo().imageRectangle.bottomLeft + [-50, 0]
	WaitFor 4, text:"Boolean", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	set BR to FoundImageInfo().imageRectangle.topRight + [50, 0]

	set allInstancesOfDistance to EveryImageRectangle(text:"Distance", SearchRectangle:[TL,BR])
	set repere to (the last item of allInstancesOfDistance).centerRight + [150, 0]
	doubleClick repere
	typeText "-42 "
	WaitFor 4, text:"-42", SearchRectangle:[repere + [-100,-25], repere + [50, 25]]
	Click text:"OK", SearchRectangle: "_lib\NX\utils".nxZones("mainZone"), waitfor:2
	WaitFor 8.0, "CAD/myFirstPart_version2"
	
	LogSuccess "Subtracting matter to the part: OK"
	LogSuccess "### Add a hole, that has a specific diameter: OK ###"
end cutCircleIntoThePiece

to updateDiameterOfCircle
	log "Updating the diameter of the circle..."
	//  TODO
	RightClick (Image:"CAD/myFirstPart_portionOfCircle", WaitFor:8)
	set TL to FoundImageLocation()
	set BR to TL + [200, 600]
	Click text:"Edit", SearchRectangle: [TL, BR], validWords:"*", waitfor:10
	wait 3 // animation
	DoubleClick {Image:"CAD/phi_diameter", WaitFor:10}
	wait 1
	typeText "40", returnKey
	WaitFor 5, text:"Scale Sketch On First Dimension", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	typeText returnKey
	selectActionFromTopRibbon "Finish"
	wait 3 // animation
	WaitFor 10.0, "CAD/myFirstPart_version3"
	LogSuccess "Updating the diameter of the circle: OK"
	
	log "Reading the new diameter..."
	Click text:"Analysis", waitFor:0, SearchRectangle: "_lib\NX\utils".nxZones("topMenu")
	selectActionFromTopRibbon "Measure"
	wait 1
	Click (Image:"CAD/myFirstPart_portionOfCircle/myFirstPart_portionOfCircle_D40", WaitFor:10)
	WaitFor 10, text:"radius", SearchRectangle: "_lib\NX\utils".nxZones("mainZone")
	set ir to FoundImageInfo().imageRectangle
	set TL to ir.topRight + [0, -5]
	set BR to ir.bottomRight + [300, 5]
	waitfor 10, text:"20.0000", SearchRectangle:[TL,BR]
	typeText escapeKey
	logSuccess "Reading the new diameter: OK"
	
end updateDiameterOfCircle

to saveThePart
	log "saving the part..."
	TypeText controlKey, "s"
	
	"_lib\system".openPowerShell
	typeText "ls C:/CAD/tmp_part.prt", returnKey
	waitfor 10, text: "Length Name"
	set ir to FoundImageInfo().imageRectangle
	set TL to ir.bottomLeft + [0, 0]
	set BR to ir.bottomRight + [300, 40]
	waitfor 10, text:"tmp_part.pr", SearchRectangle:[TL,BR]
	typeText "exit", returnKey
	logSuccess "saving the part: OK"
end saveThePart

to closeThePart
    log "Closing the file..."

    Click text:"File", waitFor:0, SearchRectangle: "_lib\NX\utils".nxZones("topMenu")
    click text:"Close", waitfor:4
    click text:"Closes all parts and keeps the session running.", waitfor:4
    set isOK to false

    //FIXME: this loop is old code, I can use "WaitForNotFound" instead
    repeat 10 times
        // FIXME: this is specific to "tmp_part.prt". OK for a demo, bad for real code

        if ImageFound(text:"tmp_part.prt", waitFor:0,SearchRectangle:"_lib\NX\utils".nxZones("topTabs")) is false then
            set isOK to true
            exit repeat
        end if
        wait 3
    end repeat
    WaitForNotFound 15, "CAD/myFirstPart_version3"

    if isOK then
        LogSuccess "Closing the file: OK"
    else
        Throw "Failed to close the file", "the file was not closed within 30 seconds"
    end if
end closeThePart